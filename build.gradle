plugins {
    id 'dev.kikugie.stonecutter'
    id 'fabric-loom' version '1.7-SNAPSHOT'
}

def scVersion = stonecutter.current.version
def props = new Properties()
rootProject.file("gradle-${scVersion}.properties").withInputStream { props.load(it) }

version = "${props.mod_version}+mc${props.minecraft_version}"
group = props.maven_group

def mcVersion = props.minecraft_version
def javaVersion = mcVersion.startsWith("1.20") ? JavaVersion.VERSION_17 : JavaVersion.VERSION_21

repositories {

    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url 'https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    
    minecraft "com.mojang:minecraft:${props.minecraft_version}"
    mappings "net.fabricmc:yarn:${props.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${props.loader_version}"
    modImplementation fabricApi.module("fabric-command-api-v2", props.fabric_api_version)
    modImplementation fabricApi.module("fabric-key-binding-api-v1", props.fabric_api_version)
    modImplementation fabricApi.module("fabric-lifecycle-events-v1", props.fabric_api_version)
    modImplementation fabricApi.module("fabric-resource-loader-v0", props.fabric_api_version)
    modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:${props.devauth_version}"

    annotationProcessor "io.github.llamalad7:mixinextras-fabric:0.4.1"
    implementation "io.github.llamalad7:mixinextras-fabric:0.4.1"
    
    modCompileOnly("com.terraformersmc:modmenu:7.2.2")
}

base {
    archivesName = props.archives_base_name
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", mcVersion
    filteringCharset "UTF-8"
    filesMatching("fabric.mod.json") {
        expand "version": project.version, "minecraft_version": mcVersion
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}
